#
# A simple theme from s0undt3ch.
# http://peepcode.com/blog/2012/my-command-line-prompt
#
# Authors:
#   Geoffrey Grosenbach <boss@topfunky.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/LhgmW.png
#

function +vi-git-status() {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]=' %F{8}✗%f'
  fi
}

function prompt_s0undt3ch_precmd {
  vcs_info
  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_s0undt3ch_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_s0undt3ch_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git hg
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' get-revision true
  zstyle ':vcs_info:*' use-simple true
  zstyle ':vcs_info:*' unstagedstr ' %F{8}✗%f'
  zstyle ':vcs_info:*' formats '  %F{8}%b%f %F{white}%.7i%f%u'
  zstyle ':vcs_info:*' actionformats ' %F{8}%b%f %F{white}%.7i%f +%a%u'
  zstyle ':vcs_info:git*+set-message:*' hooks git-status


  local user_char='❯'
  local root_char='❯❯❯'
  local success_color='%F{071}'
  local failure_color='%F{124}'

  # Define prompts.
  PROMPT="%~ %F{157}"'$python_info[virtualenv]'"%(1j;%{%F{cyan}%}%j⚙ ;) %(?..${failure_color}exitcode:%?)
%(?.${success_color}.${failure_color})${SSH_TTY:+[%n@%m]}%<<%(!.${root_char}.${user_char})%b%f "
RPROMPT='${vcs_info_msg_0_}'
}

function prompt_s0undt3ch_preview {
  local +h PROMPT='%# '
  local +h RPROMPT=''
  local +h SPROMPT=''

  if (( $# > 0 )); then
    prompt_preview_theme 's0undt3ch' "$@"
  else
    prompt_preview_theme 's0undt3ch'
    print
    prompt_preview_theme 's0undt3ch' "❯"
    print
    prompt_preview_theme 's0undt3ch' "$"
  fi
}

prompt_s0undt3ch_setup "$@"
